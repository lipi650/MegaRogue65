   40 dim ro(6,6), vs(6,6) : rem rooms, visited rooms
   45 sx=0:sy=0:ex=0:ey=0:kx=0:ky=0:cx=0:cy=0 :rem start,exit,key,current room
   50 dim r(32):dim g(32): dim b(32): rem palette values
   90 mem 1,3: screen clr
  100 gosub 7000 :rem load palette
  105 bload"fcmchrs.chr",p($40000)
  110 rem edma 0,64000,$8000000,$40000
  115 gosub 5000: rem setup fcm-rrb environment
  120 gosub 7100: rem set starting room and generate 5x5 map
  130 gosub 6500: rem  draw map
  870 getkey a$
  874 gosub 5250: rem reset settings
  875 palette restore
  900 end
 4999 rem =========setup rrb environment===========
 5000 s1=wpeek($d062):s2=wpeek($d060): rem store screen pointer
 5010 rem ==== enable seam mode ====
 5020 setbit $d054,2: setbit $d054,0 :rem fclrhi, chr16
 5030 clrbit $d031,7: poke$d058,160:  rem set 40 column mode
 5040 wpoke $d060,$0000: wpoke $d062,$5:rem set screen pointer to $50000
 5050 cc=wpeek($d05e)
 5060 poke $d05e, 80: rem set chrcnt
 5070 rem ==========================
 5080 for i=0to1999*2 step2 :wpoke$50000+i,$0020:nexti:rem fill the whole screen.
 5090 for i=0to1999*2 step2 :wpoke$ff80000+i,$0300:nexti:rem fill colour ram
 5100 getkey a$
 5110 rem === gotox ($0090) move to column 40 ($28)
 5112 rem need to put a for-next loop to do it for all rows
 5120 wpoke $50050,$0028
 5130 wpoke $ff80050,$0090
 5140 rem ===layer a new character on the already printed one
 5150 wpoke $50052,$1000 : wpoke $50054,$1001 : wpoke $50056,$1002: wpoke $50058,$1003: wpoke $5005a,$1004: wpoke $5005c,$1005
 5152 wpoke $5005e,$1006 : wpoke $50060,$1007 :wpoke $50062,$1028: wpoke $50064,$1029
 5160 wpoke $ff80052,$0100
 5170 rem === close the line by gotox and position to the far right
 5180 rem === the line can be closed anywhere earlier (eg. from $50054)
 5190 wpoke $50096,$0140
 5200 wpoke $50098,$0000
 5210 wpoke $ff80096,$0010
 5220 wpoke $ff80098,$0000
 5225 return
 5230 getkey a$
 5240 rem ===== reset settings =====
 5250 wpoke $d060,$800: wpoke$d062,$0000
 5260 clrbit $d054,2: clrbit $d054,0 :rem fclrhi, chr16
 5270 setbit $d031,7: poke$d058,80 :rem 80 column mode
 5280 return
 6499 rem === draw map ===
 6500 for y=1to5
 6505 for x=1to5
 6510  if ro(x,y)=1 then wpoke$50000+y*160+x*2+68,$1028 :rem north
 6511  if ro(x,y)=2 then wpoke$50000+y*160+x*2+68,$1029 :rem east
 6512  if ro(x,y)=3 then wpoke$50000+y*160+x*2+68,$1078 :rem n-e
 6513  if ro(x,y)=4 then wpoke$50000+y*160+x*2+68,$102a :rem s
 6514  if ro(x,y)=5 then wpoke$50000+y*160+x*2+68,$1050 :rem n-s
 6515  if ro(x,y)=6 then wpoke$50000+y*160+x*2+68,$1079 :rem s-e
 6516  if ro(x,y)=7 then wpoke$50000+y*160+x*2+68,$10a2 :rem n-e-s
 6517  if ro(x,y)=8 then wpoke$50000+y*160+x*2+68,$102b :rem w
 6518  if ro(x,y)=9 then wpoke$50000+y*160+x*2+68,$107b :rem n-w
 6519  if ro(x,y)=10then wpoke$50000+y*160+x*2+68,$1051 :rem w-e
 6520  if ro(x,y)=11then wpoke$50000+y*160+x*2+68,$10a0 :rem w-n-e
 6521  if ro(x,y)=12then wpoke$50000+y*160+x*2+68,$107a :rem w-s
 6522  if ro(x,y)=13then wpoke$50000+y*160+x*2+68,$10a3 :rem s-w-n
 6523  if ro(x,y)=14then wpoke$50000+y*160+x*2+68,$10a1 :rem e-s-w
 6524  if ro(x,y)=15then wpoke$50000+y*160+x*2+68,$1051 :rem need to add 4-way
 6530 rem poke$50000+y*160+x*2+70,$107a :rem test
 6600 next x
 6610 next y
 6620 return
 6901 rem ------------------------------
 6999 rem === load palette
 7000 dopen#5,"palette32.pal"
 7010 i=0
 7020 do while i<32
 7030 get#5,r$,g$,b$
 7040 palette color i,asc(r$),asc(g$),asc(b$)
 7050 i=i+1
 7060 loop
 7070 dclose#5
 7080 return
 7090 rem ------------------------------
 7099 rem ==== map generation ==========
 7100 sx=int(rnd(0)*5)+1:sy=int(rnd(0)*5)+1 :rem starting cell
 7105 for x=0to5
 7110 for y=0to5
 7115 vs(x,y)=0:ro(x,y)=0
 7120 next y
 7125 next x
 7130 rem room values 1n 2e 4s 8w 0,0 is upper left
 7135 ro(sx,sy)=int(rnd(0)*15)+1:vs(sx,sy)=1
 7136 if y=1 then ro(sx,sy)=ro(sx,sy)and(not(1)):rem clear doors to border
 7137 if y=5 then ro(sx,sy)=ro(sx,sy)and(not(4))
 7138 if x=1 then ro(sx,sy)=ro(sx,sy)and(not(8))
 7139 if x=5 then ro(sx,sy)=ro(sx,sy)and(not(2))
 7140 e=0: rem number of exits
 7145 if ro(sx,sy)and1 then e=e+1
 7150 if ro(sx,sy)and2 then e=e+1
 7155 if ro(sx,sy)and4 then e=e+1
 7160 if ro(sx,sy)and8 then e=e+1
 7165 if e<2 then 7135 :rem must be at least 2 exits from start cell
 7170 for r=1to5
 7175 for y=1to5
 7180 for x=1to5
 7200 if x=sx and y=sy then 7480: rem don't touch the start cell
 7210 if vs(x,y)=0 then begin
 7215 c=0
 7220  if(ro(x,y+1)and1)or(ro(x-1,y)and2)or(ro(x+1,y)and4)or(ro(x,y-1)and8) then ro(x,y)=int(rnd(0)*15)+1:vs(x,y)=1
 7230  if(ro(x,y+1)and1)then ro(x,y)=ro(x,y)or4:rem merge exits
 7232  if(ro(x+1,y)and8)then ro(x,y)=ro(x,y)or2
 7234  if(ro(x-1,y)and2)then ro(x,y)=ro(x,y)or8
 7236  if(ro(x,y-1)and4)then ro(x,y)=ro(x,y)or1
 7238 bend
 7480 next x
 7485 next y
 7490 next r
 7500 for y=1 to 5
 7510 for x=1 to 5
 7520 vs(x,y)=0
 7521 if y=1 then ro(x,y)=ro(x,y)and(not(1)):rem clear doors to border
 7522 if y=5 then ro(x,y)=ro(x,y)and(not(4))
 7523 if x=1 then ro(x,y)=ro(x,y)and(not(8))
 7524 if x=5 then ro(x,y)=ro(x,y)and(not(2))
 7530 next x
 7540 next y
 7549 rem end
 7550 return
